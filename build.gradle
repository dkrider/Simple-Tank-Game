/*
* Gradle setup for our game
* Author: Joshua Michael Waggoner (@rabbitfighter81)
*/

apply plugin: 'java'
version = '0.1.1'


/* 
* Repositories to query 
*/

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

/*
* Dependencies for program
*/

dependencies {
    compile files('libs/jar/lwjgl.jar')
}

/*
* Jar configuration
*/

jar {
    from configurations.compile.collect { zipTree it }
    manifest.attributes 'Main-Class': 'net.rabbitfighter.game.Ex8' 
}

/*
* Runs the built jar file allowing user to play the game.
*/

task runJar(type: JavaExec, dependsOn: jar) {
    main 'net.rabbitfighter.game.Ex8'
    classpath = sourceSets.main.runtimeClasspath 
}

/*
* Load proper native library path depending on OS type.
* Note: This affects all tasks with type: JavaExec
*/

tasks.withType(JavaExec) {

    def os = System.getProperty("os.name").toLowerCase()
    
    if (os.contains("windows")) { 
        systemProperty "java.library.path", file("./libs/natives/windows")
    }

    else if (os.contains("linux")) { 
        systemProperty "java.library.path", file("./libs/natives/linux")
    }

    else if (os.contains("mac")) { 
        systemProperty "java.library.path", file("./libs/natives/macosx")
    }

}

//EOF
